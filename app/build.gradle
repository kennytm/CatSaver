/*
 * CatSaver
 * Copyright (C) 2015 HiHex Ltd.
 *
 * This program is free software: you can redistribute it and/or modify it under
 * the terms of the GNU General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later
 * version.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
 * PARTICULAR PURPOSE. See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

apply plugin: 'com.android.model.application'

sourceCompatibility = 1.7
targetCompatibility = 1.7

model {
    // Workaround https://code.google.com/p/android/issues/detail?id=182249#c1.
    android {
        compileSdkVersion = 23
        buildToolsVersion = '23.0.1'

        defaultConfig.with {
            applicationId = "hihex.cs"
            minSdkVersion.apiLevel = 15
            targetSdkVersion.apiLevel = 23
            versionCode = 7
            versionName = '0.5.0'
        }
    }
    android.signingConfigs {
        debug {
            // Keystore constructed using http://stackoverflow.com/a/14035885/
            storeFile = file("aosp.keystore")
            storePassword = "android"
            storeType = "JKS"
            keyAlias = "platform"
            keyPassword = "android"
        }
        create('release') {
            storeFile = file("aosp.keystore")
            storePassword = "android"
            storeType = "JKS"
            keyAlias = "platform"
            keyPassword = "android"
        }
    }

    android.ndk {
        moduleName = 'catsaver'
        ldLibs.add 'z'
        CFlags.addAll(['-Wno-pointer-to-int-cast', '-Wno-int-to-pointer-cast'])
        abiFilters.addAll(['armeabi', 'x86'])
    }

    android.buildTypes {
        debug {
            versionNameSuffix = "-alpha"
            testCoverageEnabled = true
        }
        release {
            minifyEnabled = true
            proguardFiles.add file('proguard-rules.pro')
        }
    }
}

dependencies {
    compile 'com.nanohttpd:nanohttpd:2.1.1'             // HTTP web server
    compile 'com.google.guava:guava:18.0'               // Various utilities
    compile 'com.x5dev:chunk-templates:3.0.2'           // HTML templating
    compile 'com.moandjiezana.toml:toml4j:0.4.0'        // Filter specification
    compile 'com.github.villadora:semver:0.0.3'         // Comparing versions
}
